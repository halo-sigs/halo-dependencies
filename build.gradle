import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '3.0.0'
    id 'maven-publish'
    id 'java-platform'
}

group 'run.halo.dependencies'
description = "Halo Dependencies"

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "SonatypePackages"
            if (version.endsWith('-SNAPSHOT')) {
                url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            } else {
                url = uri("https://s01.oss.sonatype.org/content/repositories/releases/")
            }
            credentials {
                username = project.findProperty("ossr.user") ?: System.getenv("OSSR_USERNAME")
                password = project.findProperty("ossr.password") ?: System.getenv("OSSR_PASSWORD")
            }
        }
    }
    publications {
        ossr(MavenPublication) {
            from(components.javaPlatform)
        }
    }
}

javaPlatform {
    allowDependencies()
}

ext {
    springBoot = '3.0.0'
    commonsLang3 = "3.12.0"
    base62 = "0.1.3"
    pf4j = '3.8.0'
    javaDiffUtils = "4.12"
    guava = "31.1-jre"
    jsoup = '1.15.3'
    jsonPatch = "1.13"
    jasyncR2dbcMysql = '2.1.7'
    ikAnalyzer = '9.0.0'
    lucene = '9.4.1'
    openapiSchemaValidator = '1.0.7'
    springDocOpenAPI = '2.0.0'
    lombok = '1.18.22'
}

dependencies {
    // Import other BOMs
    api platform(SpringBootPlugin.BOM_COORDINATES)

    api 'org.springframework.boot:spring-boot-starter-actuator'
    api 'org.springframework.boot:spring-boot-starter-data-jpa'
    api 'org.springframework.boot:spring-boot-starter-mail'
    api 'org.springframework.boot:spring-boot-starter-thymeleaf'
    api 'org.springframework.boot:spring-boot-starter-webflux'
    api 'org.springframework.boot:spring-boot-starter-validation'
    api 'org.springframework.boot:spring-boot-starter-data-r2dbc'

    // Spring Security
    api 'org.springframework.boot:spring-boot-starter-security'
    api 'org.springframework.security:spring-security-oauth2-jose'
    api 'org.springframework.security:spring-security-oauth2-resource-server'

    api 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.0.0-RC2'
    api 'org.openapi4j:openapi-schema-validator:1.0.7'
    api "net.bytebuddy:byte-buddy"

    // Apache Lucene
    api 'org.apache.lucene:lucene-core:9.4.1'
    api 'org.apache.lucene:lucene-queryparser:9.4.1'
    api 'org.apache.lucene:lucene-highlighter:9.4.1'
    api 'cn.shenyanchao.ik-analyzer:ik-analyzer:9.0.0'

    api "org.apache.commons:commons-lang3:$commonsLang3"
    api "io.seruco.encoding:base62:$base62"
    api "org.pf4j:pf4j:$pf4j"
    api "com.google.guava:guava:$guava"
    api "org.jsoup:jsoup:$jsoup"
    api "io.github.java-diff-utils:java-diff-utils:$javaDiffUtils"
    api "org.springframework.integration:spring-integration-core"
    api "com.github.java-json-tools:json-patch:$jsonPatch"
    api "org.thymeleaf.extras:thymeleaf-extras-springsecurity6"

    // R2DBC
    // Currently, official doesn't support mssql and mariadb yet until drivers are available.
    // See https://github.com/spring-projects/spring-data-relational/commit/ee6c2c89b5c433748b22a79cf40dc8e01142caa3
    // for more.
    runtime 'io.r2dbc:r2dbc-h2'
    runtime 'com.github.jasync-sql:jasync-r2dbc-mysql:2.1.7'
    runtime 'org.postgresql:postgresql'
    runtime 'org.postgresql:r2dbc-postgresql'

    constraints {
        api "org.springdoc:springdoc-openapi-starter-webflux-ui:$springDocOpenAPI"
        api "org.openapi4j:openapi-schema-validator:$openapiSchemaValidator"

        // Apache Lucene
        api "org.apache.lucene:lucene-core:$lucene"
        api "org.apache.lucene:lucene-queryparser:$lucene"
        api "org.apache.lucene:lucene-highlighter:$lucene"
        api "cn.shenyanchao.ik-analyzer:ik-analyzer:$ikAnalyzer"

        api "org.apache.commons:commons-lang3:$commonsLang3"
        api "io.seruco.encoding:base62:$base62"
        api "org.pf4j:pf4j:$pf4j"
        api "com.google.guava:guava:$guava"
        api "org.jsoup:jsoup:$jsoup"
        api "io.github.java-diff-utils:java-diff-utils:$javaDiffUtils"
        api "com.github.java-json-tools:json-patch:$jsonPatch"
        api "org.projectlombok:lombok:$lombok"
        runtime "com.github.jasync-sql:jasync-r2dbc-mysql:$jasyncR2dbcMysql"
    }
}
