plugins {
    id 'maven-publish'
    id 'java-platform'
}

group 'run.halo.dependencies'
version '1.0.0-SNAPSHOT'
description = "Halo Dependencies"

repositories {
    mavenCentral()
}

publishing {
    repositories {
        maven {
            name = "SonatypePackages"
            if (version.endsWith('-SNAPSHOT')) {
                url = uri("https://s01.oss.sonatype.org/content/repositories/snapshots/")
            } else {
                url = mavenCentral().url
            }
            credentials {
                username = project.findProperty("ossr.user") ?: System.getenv("OSSR_USERNAME")
                password = project.findProperty("ossr.password") ?: System.getenv("OSSR_PASSWORD")
            }
        }
    }
    publications {
        ossr(MavenPublication) {
            from(components.javaPlatform)
        }
    }
}

javaPlatform {
    allowDependencies()
}

ext {
    springBoot = '3.0.0-RC2'
    commonsLang3 = "3.12.0"
    base62 = "0.1.3"
    pf4j = '3.7.0'
    javaDiffUtils = "4.12"
    guava = "31.1-jre"
    jsoup = '1.15.3'
    jsonPatch = "1.13"
    jasyncR2dbcMysql = '2.1.7'
    ikAnalyzer = '9.0.0'
    lucene = '9.4.1'
    openapiSchemaValidator = '1.0.7'
    openapiWebfluxUi = '2.0.0-RC2'
}

dependencies {
    // Import other BOMs
    api platform("org.springframework.boot:spring-boot-dependencies:$springBoot")

    constraints {
        api "org.springdoc:springdoc-openapi-starter-webflux-ui:$openapiWebfluxUi"
        api "org.openapi4j:openapi-schema-validator:$openapiSchemaValidator"

        // Apache Lucene
        api "org.apache.lucene:lucene-core:$lucene"
        api "org.apache.lucene:lucene-queryparser:$lucene"
        api "org.apache.lucene:lucene-highlighter:$lucene"
        api "cn.shenyanchao.ik-analyzer:ik-analyzer:$ikAnalyzer"

        api "org.apache.commons:commons-lang3:$commonsLang3"
        api "io.seruco.encoding:base62:$base62"
        api "org.pf4j:pf4j:$pf4j"
        api "com.google.guava:guava:$guava"
        api "org.jsoup:jsoup:$jsoup"
        api "io.github.java-diff-utils:java-diff-utils:$javaDiffUtils"
        api "com.github.java-json-tools:json-patch:$jsonPatch"

        // R2DBC
        // Currently, official doesn't support mssql and mariadb yet until drivers are available.
        // See https://github.com/spring-projects/spring-data-relational/commit/ee6c2c89b5c433748b22a79cf40dc8e01142caa3
        // for more.
        runtime "com.github.jasync-sql:jasync-r2dbc-mysql:$jasyncR2dbcMysql"
    }
}
